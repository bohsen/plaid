/*
 * Copyright 2015 Google LLC.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript { scriptHandler ->
    apply from: 'repositories.gradle',
            to: scriptHandler
    ext.versions = [
            'compileSdk'         : 30,
            'minSdk'             : 23,
            'targetSdk'          : 30,
            'appcompat'          : '1.1.0-rc01',
            'androidx'           : '1.1.0',
            'androidxCollection' : '1.0.0',
            'androidxArch'       : '2.1.0',
            'annotations'        : '1.2.0-beta01',
            'browser'            : '1.3.0',
            'constraintLayout'   : '2.0.4',
            'coreKtx'            : '1.5.0-alpha04',
            'coroutines'         : '1.4.2',
            'dagger'             : '2.30.1',
            'espresso'           : '3.4.0-alpha03',
            'extJunit'           : '1.1.0',
            'firebase_bom'       : '26.3.0',
            'firebase_gradle'    : '2.4.0',
            'glide'              : '4.9.0',
            'googleServices'     : '4.3.4',
            'gson'               : '2.8.6',
            'jsoup'              : '1.12.2',
            'junit'              : '4.13.1',
            'kotlin'             : '1.4.21',
            'ktlint'             : '0.40.0',
            'legacyCoreUtils'    : '1.0.0',
            'lifecycle'          : '2.3.0-rc01',
            'material'           : '1.3.0-rc01',
            'mockito'            : '3.7.7',
            'mockito_kotlin'     : '2.2.0',
            'okhttp'             : '4.9.0',
            'retrofit'           : '2.9.0',
            'room'               : '2.2.6',
            'supportLibrary'     : '28.0.0',
            'test_rules'         : '1.3.1-alpha03',
            'test_runner'        : '1.3.1-alpha03',
            'ui_automator'       : '2.2.0',
            'viewPager2'         : '1.0.0'
    ]
    ext.names = [
            'applicationId': 'io.plaidapp'
    ]

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-alpha05'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.google.gms:google-services:${versions.googleServices}"
        classpath "com.google.firebase:firebase-crashlytics-gradle:${versions.firebase_gradle}"

    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.26.1'
}

ext {
    // query git for the commit count to automate versioning.
    gitCommitCount = 100 +
            Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
}

subprojects {
    buildscript {
        apply from: rootProject.file('repositories.gradle')
    }

    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('scripts/copyright.kt')
        }
        groovyGradle {
            // same as groovy, but for .gradle (defaults to '*.gradle')
            target '**/*.gradle'
            paddedCell() // Avoid cyclic ambiguities
            // the Groovy Eclipse formatter extends the Java Eclipse formatter,
            // so it formats Java files by default (unless `excludeJava` is used).
            greclipse().configFile(project.rootProject.file('scripts/greclipse.properties'))
            licenseHeaderFile project.rootProject.file('scripts/copyright.kt'), '(buildscript|apply)'
        }
    }

    if (project.hasProperty( "kapt" )) {
        kapt {
            useBuildCache = true
        }
    }
}
